Module LightDMS
{
	Entity DocumentVersion
	{
		Logging 
		{
			Log LightDMS.DocumentVersion.CreatedBy;
			Log LightDMS.DocumentVersion.CreatedDate;
			Log LightDMS.DocumentVersion.DocumentID;
			Log LightDMS.DocumentVersion.VersionNumber;
			Log LightDMS.DocumentVersion.FileName;
		}
		
		Is LightDMS.DocumentEvent;
		Reference CreatedBy Common.Principal { SystemRequired; CreatedBy; }
		DateTime CreatedDate { DenyUserEdit; CreationTime; SystemRequired; }
		Guid DocumentID;
		
		Integer VersionNumber { AutocodeForEach LightDMS.DocumentVersion.DocumentID; }
		LongString FileName { Required; }
		Reference FileContent { Required; SqlIndex; }
		
		SaveMethod
		{
			OnInitialization 'DeadlockPrevention' <DeadlockPrevention.cs>;
		}
		
		ComposableFilterBy OtherUserDocumentCheckIn
		'(items, repository, filterParameter) =>
		{
			var itemList = items.Select(x => new { ID = (Guid?)x.ID, DocumentID = x.DocumentID }).ToList();
			var itemListIds = itemList.Select(x => x.ID);
			var itemListDocumentIds = itemList.Select(x => x.DocumentID).Distinct();

			return repository.LightDMS.DocumentEvent.Query()
				.Where(de =>
					itemListIds.Contains(de.DocumentVersionID) &&
					itemListDocumentIds.Contains(de.DocumentID) && // Ne micati. Dodatan uvjet za sprječavanje lockova 
					de.Extension_DocumentStatusBeforeEvent.CheckoutID != null &&
					de.Extension_DocumentStatusBeforeEvent.CheckedOutToID != de.DocumentVersion.CreatedByID)
				.Select(de => de.DocumentVersion);
		}
		';
		InvalidData OtherUserDocumentCheckIn 'Akcija nije moguća jer je sadržaj rezerviran za uređivanje od strane drugog korisnika.';
	}
	
	Parameter OtherUserDocumentCheckIn;
	
	SqlQueryable DocumentVersionExt
	"
		SELECT
			dv.ID,
			SizeInKBytes = CAST(((DATALENGTH(fc.Content) + 1024 - 1) / 1024) as decimal) -- Round to a higher integer number.
		FROM
			LightDMS.DocumentVersion dv
			LEFT JOIN LightDMS.FileContent fc ON fc.ID = dv.FileContentID
	"
	{
		Extends LightDMS.DocumentVersion;

		Decimal SizeInKBytes;
	}

	Entity FileContent
	{
		Binary Content;
		DateTime CreatedDate { DenyUserEdit; CreationTime; SystemRequired; SqlIndex; }
		Bool AzureStorage;

		ItemFilter ContentIliAzureStorage 'item => item.Content == null && item.AzureStorage != true';
		InvalidData ContentIliAzureStorage 'Ako Content nije postavljen, AzureStorage mora biti true.';
	}
	
	SqlObject FileContent_FileStream 
	"
		IF (((SELECT SERVERPROPERTY('FilestreamEffectiveLevel'))>1)
			AND ((SELECT COUNT(*) FROM sys.database_files WHERE type = 2) > 0)
			AND (SELECT is_filestream FROM sys.columns WHERE object_id = OBJECT_ID('LightDMS.FileContent') and name = 'Content') = 0)
		BEGIN
			ALTER TABLE LightDMS.FileContent
				ALTER COLUMN ID ADD ROWGUIDCOL;
			EXEC sp_rename 'LightDMS.FileContent.Content', 'Content_backup' , 'COLUMN';
			EXEC ('ALTER TABLE LightDMS.FileContent
				ADD Content varbinary(max) FILESTREAM');
		END;
		{SPLIT SCRIPT}
		IF (((SELECT SERVERPROPERTY('FilestreamEffectiveLevel'))>1)
			AND ((SELECT COUNT(*) FROM sys.database_files WHERE type = 2) > 0))
			AND EXISTS (SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'FileContent' AND COLUMN_NAME = 'Content_backup')
		BEGIN
			EXEC ('
				UPDATE LightDMS.FileContent
					SET Content = Content_backup;
				ALTER TABLE LightDMS.FileContent
					DROP COLUMN Content_backup;
			');
		END
	"
	"-- Zbog performansi ne uklanjamo filestream. Potrebno je ručno napraviti ako se radi downgrade servera 	
	"
	{
		SqlDependsOn LightDMS.FileContent.Content;
	}
}
